type file;
type script;

app (file tf, file vocab) calculate_tf (script calc_tf, string dir, file monthly)
{
    python @calc_tf dir 1 0 @tf @vocab;
}

app (file o) merger (script merge, file[] monthly)
{
    python @merge @o @filenames(monthly);
}

app (file tfidf) calculate_idf (script calc, file vocab, file tf, string key)
{
    python @calc 1 0 @vocab @tf key stdout=@tfidf;
}

app (file final) scrounge (script s, file[] i)
{
    bash @s @i stdout=@final;
}

string dir = (@arg("data","./monthly_abstracts"));

// First map stage
script calc_tf <"calculate_tf_scores.py">;
file[] all_monthly_docs <filesys_mapper; location=dir, suffix=".txt">;
file[] monthly_tf ;
file[] monthly_vocab ;
tracef("File : %s\n", @filename(all_monthly_docs[1]));
foreach monthly_doc,index in all_monthly_docs {
    tracef("File : %s\n", @filename(monthly_doc));
    file tf    <single_file_mapper; file=@strcat(@monthly_doc, ".tf")>;
    file vocab <single_file_mapper; file=@strcat(@monthly_doc, ".vocab")>;
    (tf, vocab) = calculate_tf (calc_tf, dir, monthly_doc);
    monthly_tf[index]    = tf;
    monthly_vocab[index] = vocab;
}

// First reduce stage
script merge   <"merge.py">;
file all_tf    <single_file_mapper; file=@strcat(dir, "/intermediate/TF_all.tf")>;
file all_vocab <single_file_mapper; file=@strcat(dir, "/intermediate/Vocab_all.vocab")>;
(all_tf)       = merger (merge, monthly_tf );
(all_vocab) = merger (merge, monthly_vocab );

// Second map stage
file tfidf[];
script calc_idf <"calculate_idf_scores.py">;
foreach monthly_doc, index in all_monthly_docs {
    file tfidf_t <single_file_mapper; file=@strcat(@monthly_doc, ".tfidf")>;
    (tfidf_t) = calculate_idf (calc_idf, all_vocab, all_tf, @filename(monthly_doc));
    tfidf[index] = tfidf_t;
}

// Second and last reduce stage
script concat <"concat.sh">;
file final <"final.results">;
(final) = scrounge (concat, tfidf);
